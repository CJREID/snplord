configfile:
	"config_files/snake-snippy.config.json"

sample_ids, = glob_wildcards(config['reads']+"/{sample}.R1.fastq.gz")
REF, = glob_wildcards(config['ref']+"/{reference}.fa")

print(sample_ids,)
print(REF,)

rule all:
	input:
		expand(config['reads']+"/{reference}/{sample}.out", sample=sample_ids, reference=REF),		
		expand(config['reads']+"/core/fasttree/{reference}.clean.fullcore.tree", reference=REF)

rule snippy_run:
	input:
		ref = config['ref']+"/{reference}.fa",
		r1 = config['reads']+"/{sample}.R1.fastq.gz",
		r2 = config['reads']+"/{sample}.R2.fastq.gz"
	output: 
		directory(config['reads']+"/{reference}/{sample}.out")
	conda: 
		"config_files/snippy.yaml"
	shell: 
		"snippy --outdir {output} --ref {input.ref} --R1 {input.r1} --R2 {input.r2}"


rule snippy_core:
	input:
		ref = config['ref']+"/{reference}.fa",	
	output:	
		config['reads']+"/core/{reference}.full.aln"
	conda:
		"config_files/snippy.yaml"
	shell:
		"snippy-core --prefix data/reads/core/{wildcards.reference} --ref {input.ref} data/reads/{wildcards.reference}/*.out"

rule snippy_clean:
	input:
		config['reads']+"/core/{reference}.full.aln"
	output:
		config['reads']+"/core/{reference}.clean.full.aln"
	conda: 
		"config_files/snippy.yaml"	
	shell:
		"snippy-clean_full_aln {input} > {output}"

rule gubbarnt:
	input:
		config['reads']+"/core/{reference}.clean.full.aln"
	output:
		config['reads']+"/core/gubbins/gubbins.{reference}.filtered_polymorphic_sites.fasta"
	params:
		prefix = "data/reads/core/gubbins/gubbins.{reference}"
	conda:
		"config_files/gubbins.yaml"
	shell:
		"""
		run_gubbins.py -v -p {params.prefix} {input}
		rm {wildcards.reference}.clean.full.aln.seq.joint.txt
		"""
rule snp_sites:
	input:
		config['reads']+"/core/gubbins/gubbins.{reference}.filtered_polymorphic_sites.fasta"
	output:
		config['reads']+"/core/snp_sites/{reference}.clean.fullcore.aln"
	conda: 
		"config_files/snippy.yaml"
	shell:
		"snp-sites -c {input} > {output}"

rule fasttree:
	input:
		config['reads']+"/core/snp_sites/{reference}.clean.fullcore.aln"
	output:
		config['reads']+"/core/fasttree/{reference}.clean.fullcore.tree"
	conda:
		"config_files/fasttree.yaml"
	shell:
		"fasttree -gtr -nt {input} > {output}"






